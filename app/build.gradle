apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.neenbedankt.android-apt'


def Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.huynd.skyobserver"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.huynd.skyobserver.AndroidTestRunner"
        buildConfigField "String", "API_URL", "\"https://www.atadi.vn/sapi/getprices/\""

        buildConfigField "String", "COOKIES_DATA", properties.getProperty("site.cookies", "")

        buildConfigField "String", "HEADER_HOST", properties.getProperty("headers.host", "")
        buildConfigField "String", "HEADER_CONNECTION", properties.getProperty("headers.connection", "")
        buildConfigField "String", "HEADER_CONTENT_LENGTH", properties.getProperty("headers.contentLength", "")
        buildConfigField "String", "HEADER_REQUEST_HASH", properties.getProperty("headers.requestHash", "")
        buildConfigField "String", "HEADER_ORIGIN", properties.getProperty("headers.origin", "")
        buildConfigField "String", "HEADER_REQUEST_CARRIER", properties.getProperty("headers.requestCarrier", "")
        buildConfigField "String", "HEADER_USER_AGENT", properties.getProperty("headers.userAgent", "")
        buildConfigField "String", "HEADER_CONTENT_TYPE", properties.getProperty("headers.contentType", "")
        buildConfigField "String", "HEADER_ACCEPT", properties.getProperty("headers.accept", "")
        buildConfigField "String", "HEADER_REFERER", properties.getProperty("headers.referer", "")
        buildConfigField "String", "HEADER_ACCEPT_ENCODING", properties.getProperty("headers.acceptEncoding", "")
        buildConfigField "String", "HEADER_ACCEPT_LANGUAGE", properties.getProperty("headers.acceptLanguage", "")
    }

    dataBinding {
        enabled true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

jacoco {
    toolVersion "0.7.6.201602180812"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'

    // dagger
    compile 'com.google.dagger:dagger:2.0'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Retrofit
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'

    // mockito
    androidTestCompile "org.mockito:mockito-core:2.0.5-beta"
    androidTestCompile "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:1.4"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.robolectric:shadows-support-v4:3.1.4"

    // dagger
    apt 'com.google.dagger:dagger-compiler:2.0'
    testApt "com.google.dagger:dagger-compiler:2.0"
    androidTestApt "com.google.dagger:dagger-compiler:2.0"
    provided 'org.glassfish:javax.annotation:10.0-b28'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'connectedCheck']) {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports after running tests.'

    reports {
        xml.enabled = true
        html.enabled = true
    }

    // use hidden configuration, for details look into JacocoPlugin.groovy
    jacocoClasspath = project.configurations['androidJacocoAnt']

    // exclude auto-generated classes and tests
    def fileFilter = ['**/R.class',
                      '**/R$*.class',
                      '**/*$ViewBinder*.*',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      '**/*Test*.*',
                      'com/android/databinding/**/*.*',
                      'com/huynd/skyobserver/databinding/*.*',
                      'com/huynd/skyobserver/BR.*',
                      'android/**/*.*',
                      '**/*MembersInjector*.*',
                      '**/*DaggerApplicationComponent*.class',
                      'com/huynd/skyobserver/dagger/**/*.*']
    def debugTree = fileTree(dir: "${project.buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.projectDir, includes: ['**/*.exec', '**/*.ec'])
}

tasks.coveralls {
    onlyIf { System.getenv("COVERALLS_REPO_TOKEN") }
}
